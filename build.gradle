plugins {
    id 'java'
    id 'java-library'
    id 'io.qameta.allure' version '2.10.0'
}
[compileJava, compileTestJava]*.options*.encoding = "UTF-8"

group = 'ru.egor.qa.selenidetest'
version = '1.0-SNAPSHOT'


def selenideVersion = '6.12.4',
    junitJupiterVersion = '5.9.2',
    cucumberJavaVersion = '7.12.0',
    cucumberJunitVersion = '7.12.0',
    allureVersion = '2.20.1',
    jacksonVersion = '2.14.1',
    assertjVersion = "3.23.1",
    aspectjVersion = "1.9.5"


repositories {
    jcenter()
    mavenCentral()
}

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

dependencies {
    api 'org.apache.commons:commons-math3:3.6.1'

    //JUnit
    testImplementation(
            "org.junit.jupiter:junit-jupiter-api:${junitJupiterVersion}",
            "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
    )
    //Selenide
    implementation "com.codeborne:selenide:${selenideVersion}"

    //Cucumber
    testImplementation(
            "io.cucumber:cucumber-java:$cucumberJavaVersion",
            "io.cucumber:cucumber-core:$cucumberJavaVersion",
            "io.cucumber:cucumber-junit:${cucumberJunitVersion}"
    )

    //Allure
    testImplementation(
            "io.qameta.allure:allure-cucumber7-jvm:${allureVersion}",
            "io.qameta.allure:allure-java-commons:${allureVersion}",
            "io.qameta.allure:allure-commandline:${allureVersion}",
            "io.qameta.allure:allure-junit5:${allureVersion}",
            "io.qameta.allure:allure-assertj:${allureVersion}",
            "io.qameta.allure:allure-selenide:${allureVersion}"
    )

    //Assertj
    testImplementation(
            "org.assertj:assertj-core:$assertjVersion",
            "org.aspectj:aspectjweaver:$aspectjVersion"
    )

    implementation(
            'org.jetbrains:annotations:24.0.1',
            'org.projectlombok:lombok:1.18.20',
            'com.google.guava:guava:30.0-jre'
    )
    testImplementation(
            "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}",
            'com.opencsv:opencsv:5.6',
            'org.apache.poi:poi:3.17'
    )

    testRuntimeOnly(
            "org.junit.platform:junit-platform-launcher:1.7.2",
            "org.junit.vintage:junit-vintage-engine:5.7.2")


}
task cucumberCli() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = [
                    '--plugin', 'pretty',
                    '--plugin', 'html:target/cucumber-report.html',
                    '--glue', 'ru.egor.qa.selenidetest.steps',
                    'src/test/resources']
        }
    }
}

test {
    useJUnitPlatform()
    scanForTestClasses = false
}